# Grace Compiler

## Instructions
* #### Compile and Generate jar
    - mvn clean package

* #### Run
    - java -cp target/compiler-1.0-SNAPSHOT.jar compiler.Main path

* #### Input
    - path given as an argument

### SableCC Grammar
	
###### Based on

    ⟨program⟩ ::= ⟨func-def⟩
	
	⟨func-def⟩ ::= ⟨header⟩ (⟨local-def⟩)*⟨block⟩
	
	⟨header⟩ ::= “fun” ⟨id⟩ “(” [⟨fpar-def⟩ ( “;” ⟨fpar-def⟩)*] “)” “:” ⟨ret-type⟩
	
	⟨fpar-def⟩ ::= [ “ref” ] ⟨id⟩ ( “,” ⟨id⟩)*“:” ⟨fpar-type⟩
	
	⟨data-type⟩ ::= “int” 
				| “char”
	
	⟨type⟩ ::= ⟨data-type⟩ ( “[” ⟨int-const⟩ “]” )*
	
	⟨ret-type⟩ ::= ⟨data-type⟩ 
				| “nothing”
	
	⟨fpar-type⟩ ::= ⟨data-type⟩ [ “[” “]” ] ( “[” ⟨int-const⟩ “]” )*
	
	⟨local-def⟩ ::= ⟨func-def⟩ 
				| ⟨func-decl⟩ | ⟨var-def⟩
	
	⟨var-def⟩ ::= “var” ⟨id⟩ ( “,” ⟨id⟩)*“:” ⟨type⟩ “;”
	
	⟨func-decl⟩ ::= ⟨header⟩ “;”
	
	⟨stmt⟩ ::= “;” 
		| ⟨l-value⟩ “<-” ⟨expr⟩ “;” 
		| ⟨block⟩ 
		| ⟨func-call⟩ “;”
		| “if” ⟨cond⟩ “then” ⟨stmt⟩ [ “else” ⟨stmt⟩]
		| “while” ⟨cond⟩ “do” ⟨stmt⟩ 
		| “return” [⟨expr⟩] “;”
	
	⟨block⟩ ::= “{” (⟨stmt⟩)*“}”
	
	⟨func-call⟩ ::= ⟨id⟩ “(” [⟨expr⟩ ( “,” ⟨expr⟩)*] “)”
	
	⟨l-value⟩ ::= ⟨id⟩ 
				| ⟨string-literal⟩ 
				| ⟨l-value⟩ “[” ⟨expr⟩ “]”
	
	⟨expr⟩ ::= ⟨int-const⟩ 
			| ⟨char-const⟩ 
			| ⟨l-value⟩ 
			| ⟨func-call⟩ 
			| “(” ⟨expr⟩ “)”
			| ( “+” | “-” ) ⟨expr⟩ 
			| ⟨expr⟩ ( “+” | “-” | “*” | “div” | “mod” ) ⟨expr⟩
	
	⟨cond⟩ ::= “(” ⟨cond⟩ “)” 
			| “not” ⟨cond⟩ 
			| ⟨cond⟩ ( “and” | “or” ) ⟨cond⟩
			| ⟨expr⟩ ( “=” | “#” | “<” | “>” | “<=” | “>=” ) ⟨expr⟩




###### keywords
    and char div do else fun if
	int mod not nothing or ref return
	then var while

###### Var/Func names 
    cannot be the same with keywords.
    They must conatain at least one letter of the latin alphabet and it may be followed by underscores,letters or numbers

###### number constants 
    are comprised of 0,1,2,3,4,5,6,7,8,9,0

###### character constants 
    must be in '' and can be every character and escape sequence

###### escape sequences 
        \n (line feed)
		\t  (tab)
		\r  (carriage return)
		\0 	the character with ASCII code 0
		\\\  (backslash)
		\’  (simple quotation mark)
		\”  (double quptation mark)
		\xnn the character with ASCII code nn (HEX)

###### symbolic operators 
    + − * # = < > <= >=

###### more on operators 
| 	Operator   	|Description	| Number of operands| Position-Notation |
	|:-------------:|:-------------:|:-----------------:|:-------------:|
	| + -           | 	sign		| 		1			| prefix 		|
	|* div mod      | mult ops      |		2			|   infix-left 	|
	| + -           | additive ops  |		2			|    infix-left |
	|= # > < <= >= 	|relational ops |		2			| infix-none	|
	|not 			|logical not	|		1			|prefix			|
	|and 			|logical and	|		2			|infix-left		|
	|or 			|logical or		|		2			|infix-left		|
	
 
###### separators 
    (  ) [ ] { } , ; : <−

###### one line comments 
    start with $

###### multy line comments 
    start and end with $$ 

###### supported types 
    are int and char (and respective multudimensional types)


###### Program Structure

* variable declarations 
* subprogram (function) definitions
* subprogram declarations followed by their definitions in the same scope
* variable declarations start with the keyword var ,can be many at a time
	```var i : int;
	var x, y, z : int;
	var s : char[80];
    ```

### Java Code

##### TreeTraversal
	Visitor Pattern code generated by Sablecc
##### TreeTraversal1
	Checks function,parameter and variable declarations
##### TreeTraversal2
	Checks for type-checking mostly
##### TreeTraversal3
	Code for the quad generation (Intermediate code)
##### Func
    Class to organize the functions in the given file 
##### EvalType
    Used in TreeTraversal2 for type-checking
##### Quad
    Class  for quads
##### Temp
    Class for temporary variables
##### QuadstoA
	Code for transforming the intermediate code to assembly
##### Library
	Given library for Grace

### Assembly

	The code for the assembly generation is not complete

### Test files
	src/test/
	


